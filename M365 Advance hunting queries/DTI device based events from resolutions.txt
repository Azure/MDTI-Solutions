#  Resolution tab export from DTI
#  Kusto Queries inside of M365D advanced threat hunting
#  for domain resolutions


let ThreatIntel = materialize(
    externaldata (resolve:string,location:string,network:string,autonomous:string,firstSeen:datetime, lastSeen:datetime, source:string, tags:string)
        [@'Replace-with-your-Storage-Generated-SAS-URL-Token']
        with (format='csv', ignorefirstrecord = true)
    | extend tags = split(tags, '|')
);
union (
    // Search for device click events
    ThreatIntel
    | join kind=inner (
        DeviceNetworkEvents
        | where isnotempty( RemoteUrl)
        | extend resolve = iff(RemoteUrl contains '//', tostring(parse_url(RemoteUrl).Host), RemoteUrl)
    ) on resolve
), (
    // BrowserLaunchedToOpenUrl, SmartScreenUrlWarning, ExploitGuardNetworkProtectionBlocked
    ThreatIntel
    | join kind=inner (
        DeviceEvents
        | where isnotempty( RemoteUrl)
        | extend resolve = iff(RemoteUrl contains '//', tostring(parse_url(RemoteUrl).Host), RemoteUrl)
    ) on resolve
), (
    // Mark of the Web
    ThreatIntel
    | join kind=inner (
        DeviceFileEvents
        | where isnotempty( FileOriginUrl)
        | extend resolve = iff(FileOriginUrl contains '//', tostring(parse_url(FileOriginUrl).Host), FileOriginUrl)
    ) on resolve
), (
    // Referrer Mark of the Web
    ThreatIntel
    | join kind=inner (
        DeviceFileEvents
        | where isnotempty( FileOriginReferrerUrl)
        | extend resolve = iff(FileOriginReferrerUrl contains '//', tostring(parse_url(FileOriginReferrerUrl).Host), FileOriginReferrerUrl)
    ) on resolve
), (
    ThreatIntel
    | join kind=inner (
        // DNS queries
        DeviceEvents
        | where ActionType == 'DnsQueryResponse'
        | extend resolve = tostring(parse_json(AdditionalFields).DnsQueryString)
        ) on resolve
)